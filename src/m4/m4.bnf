;;;; evol - m4.bnf
;;;; Copyright (C) 2010  Alexander Kahl <e-user@fsfe.org>
;;;; This file is part of evol.
;;;; evol is free software; you can redistribute it and/or modify
;;;; it under the terms of the GNU General Public License as published by
;;;; the Free Software Foundation; either version 3 of the License, or
;;;; (at your option) any later version.
;;;;
;;;; evol is distributed in the hope that it will be useful,
;;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;;; GNU General Public License for more details.
;;;;
;;;; You should have received a copy of the GNU General Public License
;;;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

M4 = {token}.

TOKEN = MACRO_INVOCATION 
      | QUOTED_STRING
      | DNL
      | COMMENT
      | STRING
      .

STRING = space
       | newline
       | comma
       | string
       | dnl
       | name
       | open_paren
       | close_paren
       .

DNL = dnl newline
    | dnl (space | comma | quote_start | quote_end | comment) {open_paren | close_paren | space | comma | quote_start | quote_end | macro_start | name | string | comment | dnl} newline
    .

COMMENT = comment {open_paren | close_paren | space | comma | quote_start | quote_end | macro_start | name | string | comment | dnl} newline.

QUOTED_STRING = quote_start {dnl | string | comment | macro_start | name | open_paren | close_paren | space | newline | comma | QUOTED_STRING} quote_end.

MACRO_START = (dnl | macro_start) {name | macro_start | dnl}+.

MACRO_INVOCATION = MACRO_START
                 | MACRO_START open_paren {MACRO_INVOCATION | QUOTED_STRING | DNL | COMMENT | space | newline | comma | string | name} close_paren.

space = " ".

newline = "\\n".

open_paren = "\(".

close_paren = "\)".

comma = ",".

quote_start = "`".

quote_end = "'".

comment = "#".

dnl = "dnl".

macro_start = "[_a-zA-Z]".

name = "\\w+".

string = "[^ \\n(,`'#]+".
